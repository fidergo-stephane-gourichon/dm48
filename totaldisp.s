%%HP: T(0)A(R)F(.);
"%This file is licensed
%under GPL v2

%TOTALDISP.S

%NIV1: INFOSTRING
%NIV2: GROBTEMP129*62
%NIV3: GROB131*64DEST
%NIV4: MAPSTRING
%NIV5: LL COMPLEMENT
%NIV6: LIB DE MURS
%(NIV7: OBLIBSAC)
%NIV8: FLUXPOSES
%NIV9: FLUXVOLANT
%NIV10: LLOBSPR
%NIV11: FLUXMONSTRES
%NIV12: LLMONSTRESPR
%...

GOSBVL 6385D
GOSUB PREPFOND

%CASE EN COURS:Bs
B=0 S
B=B-1 S %16 CASES
%AU DEBUT B=15,EN FIN:
%B=0(MUR NON DESSINÉ)

*GOCASEN
%1:CALCULER X et Y de
%LA CASE VISÉE 
GOSUB GOHOME
%dXXYY
%D1

C=B S %N° DE CASE
P=C 15 %PAS P-1

GOSUB TABLDY
$77
$00FF1000FF10
$00FF10EF2000FF10EF20
*TABLDY
C=RSTK
C+P+1
C+P+1
D0=C %D0 POINTE DY
A=DAT0 B
B=A B %Bb=DY

GOSUB TABLDX
$7
$0001112222233333
*TABLDX
C=RSTK
C+P+1
D0=C %D0 POINTE DX
A=DAT0 S
R4=A S
A=0 B
ASLC %Ab=DX

%A=DX B=DY
P= 0

C=DAT1 A %dj.x.X.y.Y
?CBIT=1 1
GOYES DJ1x
%DJ=0x  -X et -Y
A=-A B
B=-B B
*DJ1x
?CBIT=0 0
GOYES DJx0
%DJ=x1  *i
A=-A B
ABEX B
*DJx0

%A=DX,B=DY
%IL FAUT ADDITIONNER

%C=DAT1 A %C=YYXXD
%DÉJA FAIT

CSRC %Cs=dj,Ca=_YYXX
C=C+A B
CSRC
CSRC %C=000YY
C=C+B B
C=0 XS
CSLC
CSLC
%Ca=0YYXX,Cs=dj

%AJOUT POUR MEMORISER
R3=C S %=dj
%DSRC %Ds=DY,Dr=dj

%MAINTENANT QU'ON A XY
%LOOKONS.JE VEUX
%CONNAITRE qg ET qd.

R3=C A
GOSUB C4LISCASE
GOTO QGQD&FL01CONNUS
*BxLISCASE
%LC2MAPSTR
LC 03
GOSUB GOLEVCb
A=0 A
A=DAT1 B %A=000LR
%R2=A A %%R2=000(LAR)b
C=0 A
C=B X
GOTO BxLISCASEsuite
*C4LISCASE
%LISCASE PREND C=YYXX
D=C A
%RESSORT QGQD ET MET
%R4x/a=N°CASELUE ET
%MET FLAGS:
%0 VALIDEJOUEUR
%1 VALIDEMONSTRE
%2 PRESENTOV&M
%3 PRESENTOBJFIXE

%FLAGS A PRIORI
ST=0 0%PASVALIDEJOUEUR
ST=0 1%PASVALIDEMONSTRE
ST=0 2%PAS PRESENTOV&M
ST=0 3%PAS PRESENTOBJFIXE

%ON CALCULE X*LAR+Y
%TROUVONS LAR
%LC2MAPSTR
LC 03
GOSUB GOLEVCb

%D=0YYXX
A=0 A
A=DAT1 B %A=000LR
%R2=A A %%R2=000(LAR)b
C=D A %C=*YYXX
CSRC
CSRC %C=___YY
?CŠA B
GOYES CASEHORSLABY
CSLC
CSLC %C=_YYXX
B=0 A %RESULT‰1000h
P= 10 %16-10=6BIT MULTIP
*MULTIP
SB=0
CSRB B
?SB=0
GOYES MULT1
B=B+A A
*MULT1
A=A+A A
P=P+1
GONC MULTIP

%B=X*LR
LC 0FFF
?B>C A
GOYES CASEHORSLABY
%Ca=0
%D=0YYXX
%ICI VÉRIF LAR<64
A=0 X
?A=0 A
GOYES OUFLAR<64
LC 20
GOTO ERRORCb
*OUFLAR<64
C=D A
CSR A
CSR A
C=0 XS
C=C+B X
%Ca=X*LAR+Y=N°CASE
R4=C A %X
%RESTE À LIRE QUARTET

*BxLISCASEsuite
D1=D1+ 2
A=DAT1 A %OFFSET
%Ca=X*LAR+Y,A=OFFSET
?C<A A
GOYES OKCASEDEDANS
*CASEHORSLABY
%FLAGS 0123 OFF.OK
LC 10
B=C B %B=qgqd
A=0 A
R0=A A
R1=A A
A=-A-1 A
R4=A A
LA 3F
A=A&C B
%GOTO QGQD&FL01CONNUS
RTN

*OKCASEDEDANS
B=A A
D1=D1+ 5
%B=OFS C=NUMCASE
AD1EX
A=A+C A %A=ADRQG
D1=A
R0=A A %%R0=QG
A=A+B A %A=ADRQD
R1=A A %%R1=QD
C=DAT1 1
CSL B
D1=A
C=DAT1 1

%C=[][]#0qgqd
%ENFIN ON CONNAIT QGQD

%!TRAITER LE CAS
%SPECIAL
LA D0
?A‹C B
GOYES PAS.SPECIAL
LC FF
GOTO ERRORCb
*PAS.SPECIAL

B=C B %B=qgqd
LA 3F
A=A&C B

LC 1B
?A=C B %ESCALIER?
GOYES LUESCALIER
LC 11
?A=C B %PSECRET?
GOYES LUVALIDE
?ABIT=1 5 %PORTE
GOYES LUVALIDE
?ABIT=1 4 %TOUS MURS
RTNYES %4FLAGS A 0
*LUVALIDE
ST=1 0 %VALIDE JOUEUR %%
ST=1 1 %VALIDE OBJ&M
C=B B
?CBIT=1 7
GOYES PASPRESENTOV&M
ST=1 2
*PASPRESENTOV&M
?CBIT=1 6
GOYES PASPRESENTOBJFIXE
ST=1 3
*PASPRESENTOBJFIXE
RTN
*LUESCALIER
ST=1 0 %VALIDE JOUEUR
%MAIS PAS MONSTRE
ST=1 2 %OBJETS POSSIBLES
ST=1 3 %VOLANT&FIXES
RTN
*QGQD&FL01CONNUS
?ST=1 0
GOYES CASEVALIDEOK
?B‹0 S
GOYES OUF.BADCASE‹MOI
LC 21
GOTO ERRORCb
*OUF.BADCASE‹MOI

*CASEVALIDEOK
%DECODONS CASE
ST=0 0 %NOTELEP(EX-VALID)

%B=qgqd,A=qgqd&3F
LC 1B
?A=C B
GOYES STAIRS
GOTO NOSTAIRS
*STAIRS
%DESSINER STAIRS‹MURTEL
C=B S

GOSUB GETCbDIRREL
%SAUVE Bb
BSRC
BSRC

C=C-1 S
GOC PASESCALPASVISIBLE

%0FACE 1GAUCHE 2DERRIERE
C=C-1 B
GONC ESCALPASDIR0
ST=0 7
GOTO FINIRESCALIER
*ESCALPASDIR0
C=C-1 B
GONC ESCALPASDIR1
GOTO PASESCALPASVISIBLE
*ESCALPASDIR1
C=C-1 B
GONC ESCALPASDIR2
ST=1 7
GOTO FINIRESCALIER
*ESCALPASDIR2
GOTO PASESCALPASVISIBLE

*FINIRESCALIER

%GROBDEST=NIVEAU 2
GOSBVL 6385D
D1=D1+ 5
A=DAT1 A
B=A A %B=ADR GROB

%STAIRLIB=NIVEAU 5
%LC2STAIRL
LC 19
GOSUB GOLEVCb
C=0 A
CSLC %C=0000n
?ST=0 7
GOYES ESCPASVERSBAS
C=C+15 B
*ESCPASVERSBAS
%RÉCUP Ca ème STRING
%DANS LL POINTÉ PAR D1
GOSUB D1LLCaGET
GOC PASESCALPASVISIBLE
%OK.C=DEBSTR

A=B A
%A=GROBDEST
%C=DEBSTR
%OK POUR DESSINER STAIR
GOSUBL AGrCmurREPL

*PASESCALPASVISIBLE
%RECUP QGQD
BSLC
BSLC
%B=qgqd

C=B S
R0=C S
ST=0 4 %OBJETS DERRIERE
ST=1 5 %NE PAS MODIF FLAG
GOSUBL DROBJ
ST=1 4 %OBJETS DEVANT
ST=1 5 %NE PAS MODIF FLAG
GOSUBL DROBJ
C=R0 S
B=C S
GOTO FINDECASE

*NOSTAIRS
ST=0 1 %PAS PORTE
?ABIT=0 5
GOYES PAS.DE.PORTE
ST=1 1 %PORTE!
GOTO DRAWTOUT
*PAS.DE.PORTE
%B=qgqd,A=qgqd&3F
?ABIT=1 4
GOYES PASNOWALL
GOTO NOWALL
*PASNOWALL
LC 11 %SECRET
?A‹C B
GOYES PAS.PASSAGE.SEC
%NOUS OBSERVONS UNE
%CASE 'PASSAGE SECRET'
?B=0 S
GOYES INSIDESECRET
%-SI Bs‹0  DE LOIN
GOSUBL DESSINEMURTEL
*COMMEMURNU

GOTO FINDECASE
*INSIDESECRET
%-SI Bs=0,ON EST À
%L'INTERIEUR 
%DESSINER OBJETS...
%DU CAS STANDARD
GOTO DRAWTOUT
*PAS.PASSAGE.SEC
GOSUBL DESSINEMURTEL
LA 0F
A=A&B B
%B=qgqd,A=0QD

?A=0 B %MURNU
GOYES COMMEMURNU

?ABIT=0 3
GOYES PASESCALIERSERRURELEVIER
%ESCALIERSERRURELEVIER
?ABIT=1 2
GOYES LEVIERINTERRUPT
%SERRURE
LC FD
GOTO ERRORCb
*LEVIERINTERRUPT
%INTERRUPT 2POS 2TYPES
LC FC
GOTO ERRORCb

*PASESCALIERSERRURELEVIER

?ABIT=1 2
GOYES OUFPASSPECIAL
%UN DES 4 CAS
%0MUR (DEJA FAIT)
%1SECRET (DEJA AUSSI)
%2?
%3?
LC F8
GOTO ERRORCb

*OUFPASSPECIAL

ABIT=0 2
%B=qgqd,A=0QD (A‰3)
A=A-1 B
GONC NODISCRET
%DISCRET
GOSUB GETCbDIRREL
?C‹0 B
GOYES DISCPASVISIBLE
%LCEXC-GRTEMP
LC 013
CSRC
?C‹B S
GOYES DISCPASVISIBLE
GOSUB GOLEVCb
LA 003CC
A=A+C A
D1=A
LC 00022B
DAT1=C 1
CSRC
A=A+C A
D1=A
LC C
DAT1=C 1
*DISCPASVISIBLE
*INSCRPASVISIBLE
GOTO FINDECASE

*NODISCRET
A=A-1 B
GOC INSCRIPTION
GOTO NOINSCRIPTION
*INSCRIPTION
LC 3
CSRC
?B=C S
GOYES INSCRDEVANT
%LC2INSCRL
LC 14
D=C B
GOTO COMMEFONTAINE
*INSCRDEVANT
GOSUB GETCbDIRREL
?C‹0 B
GOYES INSCRPASVISIBLE
C=R3 A
B=C A
%LC2NUMMSG
LC 0A
GOSUBL GOLEVCb
P= 1
GOSUBL SEEKB4ATFLUC
GONC OKNUMINSCRFOUND
LC ED
GOLONG ERRORCb
*OKNUMINSCRFOUND
C=0 A
C=DAT0 B
R0=C A
GOSBVL 067D2
GOSBVL 06537
GOSUB EXTFOLLOWNOREG
'WRITE
$CCD20
GOIN5 FINBIGCODE
GOSUBL SAVEREG
B=0 S
B=B+1 S
B=B+1 S
B=B+1 S
GOTO FINDECASE
*EXTFOLLOWNOREG
C=RSTK
D0=C
GOVLNG 05149
*NOINSCRIPTION
A=A-1 B
GOC FONTAINE
GOTO NOFONTAINE
*FONTAINE
%LC2FONTALIB
LC 13
D=C B


*COMMEFONTAINE
%SUFFIT CONSERVER Bs
%1:VOIR DIRECTION RELATIVE
%2:VOIR SI CASSOS
%  OU SI METTRE FLAG

GOSUB GETCbDIRREL
%0FACE 1GAUCHE 2DERRIERE
%FINRELDIR

?C‹0 B
GOYES PASDEFACE
%DEFACE
ST=1 7
GOTO FINIRFONTAINE
*PASVISIBLEDICI
*ENFAITNONPASDEFONTAINE
GOTO FINDECASE
*PASDEFACE
%POSSIBLE 1(G)2(-)3(D)
CSRC
A=C S
C=B S
P=C 15
%LC 3131031310310310
LC 0130130131301313
P= 0
?C‹A S
GOYES PASVISIBLEDICI
%!C'ESTLABONNE!
ST=0 7 %2EPARTIE LIB
*FINIRFONTAINE
C=D B
GOSUB GOLEVCb
C=0 A
?ST=0 7
GOYES PASAVANCER
C=C+1 B
*PASAVANCER
C=B S
CSLC %C=0000n
%RÉCUP Ca ème STRING
%DANS LL POINTÉ PAR D1
GOSUB D1LLCaGET
GOC ENFAITNONPASDEFONTAINE
%OK.C=DEBSTR
D0=C

GOSUB FINTABLXYCENTR
%« { (-2,32) (86,32) (20,32) (64,32) (42,32) (-22,37) (106,37) (10,37) (74,37) (42,37) (-22,53) (106,53) (42,53) (0,0) (0,0) (0,0) } REVLIST 1 « (32,-1) + CR RB A~H 1 2 SUB SWAP RB A~H 1 2 SUB + » DOLIST …LIST »
$00020002000243A443A843A042A442A642A242A842A0F1A4F106F143F167F1E1
*FINTABLXYCENTR
A=B S
A=0 A
ASLC %A=0000n
A=A+A B
A=A+A B
C=RSTK
C=C+A A
D1=C
C=0 A
C=DAT1 4

C=B S
R0=C S
GOSUBL D0SPREPLCaXY
C=R0 S
B=C S
GOTO FINDECASE
*GETCbDIRREL
%RELDIR
C=R3 S %DJ
CSLC
C=C+C B
C=C+C B
CSL B
C=C-B B
C=-C B
CSR B
CSRB B
CSRB B
%C=PM-DJ
%0FACE 1GAUCHE 2DERRIERE
RTN

*NOFONTAINE
A=A-1 B
GONC NOPOUSSOIR
LC FA
GOTO ERRORCb
*NOPOUSSOIR
LC F6 %PASNORMAL!
GOTO ERRORCb

*NOWALL
%B=qgqd,A=x
?ABIT=1 3
GOYES NW.PAS.STD
?ABIT=0 2
GOYES NOTRAPPE
%DESSINE TRAPPE
%LC2TRAPLB
LC 1B
GOSUB DRAWPLAQUELIKE
%FABULAL!
*NOTRAPPE
*APRESPLAQUE
?ABIT=0 0
GOYES NODESSOUS
%DESSINE DESSOUS
%LC2STRAPL
LC 1C
GOSUB DRAWPLAQUELIKE
*NODESSOUS
%ST=0 0NOTELEP...
?ABIT=0 1
GOYES NOSETTELEPORT
%ON MET FLAG TELEPORT
ST=1 0
*NOSETTELEPORT
GOTO DRAWTOUT
*NW.PAS.STD
%B=qgqd,A=x
?ABIT=1 2
GOYES NOPLAQUE
%DESSINE PLAQUE
%LC2PLAQLB
LC 1A
GOSUB DRAWPLAQUELIKE

GOTO APRESPLAQUE
*DRAWPLAQUELIKE
%LA ROUTINE NE MODIFIE
%PAS D
CDEX B %GARDER POSPILE
C=B S
BSLC
BSL A
BSL A
B=A B
C=B A
CDEX A %ETVOILA

%MAINTENANT ON PEUT BOSSER
%PLAQUELIB=NIVEAU 11
GOSUB GOLEVCb
C=0 A
CSLC %C=0000n
%RÉCUP Ca ème STRING
%DANS LL POINTÉ PAR D1
GOSUB D1LLCaGET
GOC ENFAITNONPASDESPRITE
%OK.C=DEBSTR
D0=C
LC 04A3D

GOSUBL D0SPREPLCaXY

*ENFAITNONPASDESPRITE
%RECUP.REG
%Bs=N°CAZ,Ab,Bb
C=D A
B=C A
A=B A
BSR A
BSR A
BSRC
RTN

*NOPLAQUE
%DONC PORTE.EN.BOIS.EXPLO
ST=1 1 %COMMANDER PORTE
%GOTO DRAWTOUT
*DRAWTOUT

C=B S
R0=C S

ST=0 4 %OBJETS DERRIERE
ST=0 5 %OK POUR MOD FLAG
GOSUB DROBJ
%PORTE
%MONSTRE
GOSUBL DRMONSTRE
ST=1 4 %OBJETS DEVANT
ST=0 5
GOSUB DROBJ

%NUAGES


C=R0 S
B=C S

%TELEPORT
?ST=0 0
GOYES NODESSINETELEP
ST=1 10 %DISP.PERIODIQUE
GOSUB DESSINEMURTEL
*NODESSINETELEP
*FINDECASE

B=B-1 S
GOC FINILES16
GOTO GOCASEN
*FINILES16

%ENSUITE FAUDRA
%RECOPIER LE GROBTEMP2
%SUR LE GROBDEST3
GOSBVL 6385D
D1=D1+ 5
C=DAT1 A
C=C+10 A
C=C+10 A
D0=C
D1=D1+ 5
A=DAT1 A
LC 00036
A=A+C A
D1=A
%RECOPIER 62 LIGNES
%DE D0 VERS D1
LC 3D
*RECOPIE
A=DAT0 W
ABIT=1 0
DAT1=A W
D0=D0+ 16
D1=D1+ 16
A=DAT0 A
DAT1=A A
D0=D0+ 16
D0=D0+ 2
D1=D1+ 16
D1=D1+ 2
C=C-1 B
GONC RECOPIE

GOLONG SORTIE.DE.TOTALDISP
'ERREUR.S %FIN:GOVL
'GOROUTINES.S %FIN:RTN
'LLGET.S %FIN:RTN
'NPUTFOND.S %FIN:RTN
*DESSINEMURTEL
%YAMURTEL
%YA VRAIMENT MUR?
C=B S
C=C-1 S
GONC OKPOURMUR
%DONC Bs=0
%SI ST=0 0 ERREUR
%(I.E. DANS MUR)
%SINON ON FAIT CHAMP
?ST=1 0
GOYES OKINCHAMP
LC F5
GOTO ERRORCb
*OKINCHAMP
%NE PAS MODIF Bb
BSRC
BSRC
GOSBVL 6385D
D1=D1+ 5
C=DAT1 A
D0=C
D0=D0+ 10
D0=D0+ 10
LC 3D
B=C B
*1LINEINSIDE
P= 9
GOSUBL QCTELEPORT
D0=D0+ 10
P= 10
GOSUBL QCTELEPORT
D0=D0+ 10
D0=D0+ 14
B=B-1 B
GONC 1LINEINSIDE
P= 0
BSLC
BSLC
GOTO INSIDE.TELEP.END
*OKPOURMUR
%DESSINER MURTEL
%SAUVE Bb
BSRC
BSRC
%GROBDEST=NIVE U 2
GOSBVL 6385D
D1=D1+ 5
A=DAT1 A
B=A A %B=ADR GROB

%LC2MURLIB
LC 12
GOSUB GOLEVCb
C=0 A
CSLC %C=0000n
%RÉCUP Ca ème STRING
%DANS LL POINTÉ PAR D1
GOSUB D1LLCaGET
%OK.C=DEBSTR

A=B A
%A=GROBDEST
%C=DEBSTR
%OK POUR DESSINER MURTEL
GOSUBL AGrCmurREPL

%RECUP QGQD
BSLC
BSLC
%B=qgqd
*INSIDE.TELEP.END
RTN
'DROBJ.S
'DRMONSTRE.S
'SEEK.S
'W&T.REPL.S
*SORTIE.DE.TOTALDISP
@"